# Copyright Â© 2007-2018 ANSSI. All Rights Reserved.
# Distributed under the terms of the GNU General Public License v2

EAPI="4"
DESCRIPTION="Hardware configurations for clip-installer"
HOMEPAGE="https://www.ssi.gouv.fr"
SRC_URI="mirror://clip/${P}.tar.xz"

LICENSE="LGPL-2.1+"
SLOT="0"
KEYWORDS="x86 ~amd64 ~arm"
IUSE="clip-livecd clip"

DEPEND=""
RDEPEND="
	clip-livecd? (
		sys-apps/dmidecode
		sys-apps/hdparm
		sys-apps/lshw
		sys-apps/kmod
		sys-apps/net-tools
		sys-apps/pciutils
		sys-apps/sdparm
		sys-apps/smartmontools
		sys-apps/usbutils
		x86? ( x11-misc/read-edid )
	)
"

src_install() {
	local hw name core nb_main=0 nb_extra=0
	for hw in "${S}/profiles"/*; do
		[ -d "${hw}" ] || continue
		name="$(basename -- "${hw}")"
		insinto "/usr/share/${PN}/profiles/${name}"
		doins "${hw}"/*
		if [[ -d "${hw}/scripts" ]]; then
			doins -r "${hw}/scripts"
		fi
		core="${name//+}"
		core="${core//-}"
		if [ "${name}" != "${core}" ]; then
			let "nb_extra++"
		else
			let "nb_main++"
		fi
	done
	einfo "Installed ${nb_main} main configurations and ${nb_extra} derivatives"

	insinto "/usr/share/${PN}/scripts"
	doins "${S}/scripts"/*

	if use clip-livecd; then
		dobin "${WORKDIR}/${P}/bin"/*
	elif use clip; then
		exeinto "/usr/share/${PN}/scripts"
		doexe "${S}/scripts/"*
		newinitd "${FILESDIR}/hwconfig.initd" "hwconfig"
	fi
}
