# Based on baselayout-lite-1.0_pre1.ebuild, which is 
# Copyright 1999-2006 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2

EAPI=5

DESCRIPTION="Baselayout for update view"
HOMEPAGE="https://www.ssi.gouv.fr"
SRC_URI="mirror://clip/${P}.tar.xz"

LICENSE="LGPL-2.1+"
SLOT="0"
KEYWORDS="x86 ~amd64"
IUSE=""

# Provide a virtual linux-sources as well here to fix
# some borken run depends checking
PROVIDE="virtual/baselayout virtual/linux-sources"

src_install() {
	if [[ ! ${VERY_BRAVE_OR_VERY_DUMB} == "yes" ]] && [[ ${ROOT} == "/" ]] ; then
		ewarn "This should not be emerged to your root system..."
		die "silly options will destroy your system"
	fi
	
	keepdir /bin /etc /home /sbin /usr /var /root 
	keepdir /proc /opt /tmp

	if use amd64; then
		keepdir /lib64
		dosym lib64 /lib
		keepdir /usr/lib64
		dosym lib64 /usr/lib
	else
		keepdir /lib
	fi
	fperms 700 /root
	fperms 1777 /tmp
	keepdir /var/run
	fperms 1777 /var/run

	keepdir /user_root
	keepdir /audit_root

	keepdir /usr/local

	# /etc stuff
	keepdir /etc/core /etc/admin
	# Needed as workaround for portage.py : tries to create the directory
	# if not os.path.exists() which is the case with a dangling symlink
	cd ${D}/etc || die "Cannot cd to etc"
	cp -RPp ${S}/etc/* .
	dosym core/localtime /etc/localtime

	dodir /usr/bin

	into /
	dobin "${S}/bin/verictl.sh"
}
